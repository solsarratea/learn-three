<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8">
		<title>My first three.js app</title>
        <script src="/three.min.js"></script>
        <script type="text/javascript" src="dat.gui.min.js"></script>
  </head>
	<style>
		body { margin: 0; }
		canvas { display: block; }
	</style>
	</head>
	<body>  
    <script type="x-shader/x-vertex" id="colorize">
        uniform vec2 resolution;
        uniform sampler2D texture;
        uniform vec3 color1;
        uniform vec3 color2;
        uniform vec3 color3;

        float map(float value, float inMin, float inMax, float outMin, float outMax) {
              return outMin + (outMax - outMin) * (value - inMin) / (inMax - inMin);
        }

        void main(void)
        {
            vec2 uv = (gl_FragCoord.xy / resolution); 
            
            vec3 black = vec3(0.0, 0.0, 0.0);
            vec3 targetColor = vec3(0.0, 0.0, 0.0);
            float sourceRamp = texture2D( texture, uv ).g * 3.0;

            if (sourceRamp < 0.1) 
            {
                // targetColor = black;

                float ramp = map(sourceRamp, 0.0, 0.1, 0.0, 1.0);
                targetColor = mix( black, color3, min( ramp, 1.0 ) );
            }
            else if (sourceRamp < 0.45) 
            {
                float ramp = map(sourceRamp, 0.1, 0.45, 0.0, 1.0);
                targetColor = mix( color3, color2, min( ramp, 1.0 ) );
            }
            else
            {
                float ramp = map(sourceRamp, 0.45, 1.0, 0.0, 1.0);
                targetColor = mix( color2, color1, min( ramp, 1.0 ) );
            }
            
            gl_FragColor = vec4( targetColor, 1.0);
        }  
  </script>
  <script type="x-shader/x-fragment" id="fragShader">
    uniform vec2 res;
    uniform sampler2D bufferTexture;
    uniform sampler2D start;
    uniform vec3 brush;
    uniform float time;

    uniform float dA;
    uniform float dB;
    uniform float feed;
    uniform float k;

    uniform float brushSize;
    uniform float flow;

    uniform int clear;
    uniform bool enableBrush;

    uniform float diff1;
    uniform float diff2;

    int count = 0;

    void main() 
    {
        // load current values for a and b
        if ( clear == 1) {
            gl_FragColor = texture2D(start, gl_FragCoord.xy / res.xy);
            return;
        }
        vec4 currentColor = texture2D(bufferTexture, gl_FragCoord.xy / res.xy);
        float a = currentColor.r;
        float b = currentColor.g;

        if (enableBrush){
            float dist = distance(brush.xy, gl_FragCoord.xy);
            if (dist < brushSize)  {
                float ratio = 1.0 - dist/brushSize;
                b += 0.5 * ratio * brush.z;
            }
        }
       

        if (clear == 1) {
            a = 1.0;
            b = 0.0;
        }

        float offset = 1.0;

        // get pixels from surrounding grid
        vec4 N = texture2D(bufferTexture, vec2(gl_FragCoord.x/res.x, gl_FragCoord.y/res.y - offset/res.y));
        vec4 S = texture2D(bufferTexture, vec2(gl_FragCoord.x/res.x, gl_FragCoord.y/res.y + offset/res.y));
        vec4 E = texture2D(bufferTexture, vec2(gl_FragCoord.x/res.x + offset/res.x, gl_FragCoord.y/res.y));
        vec4 W = texture2D(bufferTexture, vec2(gl_FragCoord.x/res.x - offset/res.x, gl_FragCoord.y/res.y));

        vec4 NE = texture2D(bufferTexture, vec2(gl_FragCoord.x/res.x + offset/res.x, gl_FragCoord.y/res.y + offset/res.y));
        vec4 NW = texture2D(bufferTexture, vec2(gl_FragCoord.x/res.x - offset/res.x, gl_FragCoord.y/res.y + offset/res.y));
        vec4 SE = texture2D(bufferTexture, vec2(gl_FragCoord.x/res.x + offset/res.x, gl_FragCoord.y/res.y - offset/res.y));
        vec4 SW = texture2D(bufferTexture, vec2(gl_FragCoord.x/res.x - offset/res.x, gl_FragCoord.y/res.y - offset/res.y));

        // calculate laplace of A
        float lapA = 0.0;
        lapA += a * -1.0;
        lapA += N.r * diff1;
        lapA += S.r * diff1;
        lapA += E.r * diff1;
        lapA += W.r * diff1;
        lapA += NE.r * diff2;
        lapA += NW.r * diff2;
        lapA += SE.r * diff2;
        lapA += SW.r * diff2;


        // calculate laplace of B
        float lapB = 0.0;
        lapB += b * -1.0;
        lapB += N.g * diff1;
        lapB += S.g * diff1;
        lapB += E.g * diff1;
        lapB += W.g * diff1;
        lapB += NE.g * diff2;
        lapB += NW.g * diff2;
        lapB += SE.g * diff2;
        lapB += SW.g * diff2;


        // calculate diffusion reaction
        a += ((dA * lapA) - (a*b*b) + (feed * (1.0-a))) * 1.0;
        b += ((dB * lapB) + (a*b*b) - ((k + feed) * b)) * 1.0;


        a = clamp(a, 0.0, 1.0);
        b = clamp(b, 0.0, 1.0);

        vec4 newColor = vec4(a, b, 0.0, 1.0);
        gl_FragColor = newColor;
    }

  </script>
  <script type="text/javascript" src="grayscott.js"></script>
  <meta charset="UTF-8">
  <style>
      #gradient
      {
          height: 40px;
          width: 85%;
          margin-top: 5px;
          margin-left:auto;
          margin-right:auto;
      }
  </style>
	</body>
</html>